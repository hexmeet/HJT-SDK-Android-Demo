apply plugin: 'com.android.application'


android {
    compileSdkVersion 28
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId System.getenv("APPLICATION_ID") as String ?: "com.hexmeet.hjtsdkdemo"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode System.getenv("BUILD_CODE") as Integer ?: 0
        versionName System.getenv("BUILD_VERSION") as String ?: "0.0"

        def appFileProviderAuth = applicationId + (".fileprovider")
        def wxAppId = System.getenv("WX_APPID") as String ?: "wx_id_of_your_company"
        def trialApplicationURL = System.getenv("TRIAL_APPLICATION_URL") as String ?: "http://www.hjtcloud.com/try"
        def cloudLocationServerAddr = System.getenv("CLOUD_LOCATION_SERVER_ADDR") as String ?: "cloudcenter.hexmeet.com"
        def cloudServerAddr = System.getenv("CLOUD_SERVER_ADDR") as String ?: "cloud.hexmeet.com"
        def cloudServerProtoHttps = System.getenv("CLOUD_SERVER_PROTOCOL_HTTPS") as String ?: "true"

        manifestPlaceholders = [
                APP_FILE_PROVIDER_AUTH: appFileProviderAuth,
                JPUSH_PKGNAME : applicationId, //JPush上注册的包名.
                JPUSH_APPKEY : "yours", //JPush上注册的包名对应的appkey.

                HUAWEI_APPID:"yours ",//华为平台注册的appid

                XIAOMI_APPKEY:"MI-yours",//小米平台注册的appkey
                XIAOMI_APPID:"MI-yours",//小米平台注册的appid

                OPPO_APPKEY : "OP-yours", // OPPO平台注册的appkey
                OPPO_APPID : "OP-yours", // OPPO平台注册的appid
                OPPO_APPSECRET: "OP-yours",//OPPO平台注册的appsecret

                MEIZU_APPKEY:"MZ-yours",//魅族平台注册的appkey
                MEIZU_APPID:"MZ-yours",//魅族平台注册的appid

                VIVO_APPKEY :"yours", // VIVO平台注册的appkey
                VIVO_APPID :"yours", // VIVO平台注册的appid

                JPUSH_CHANNEL:"developer-default", //暂时填写默认值即可.
        ]


        buildConfigField("String", "WX_APPID", '"' + wxAppId + '"')
        buildConfigField("String", "TRIAL_APPLICATION_URL", '"' + trialApplicationURL + '"')
        buildConfigField("String", "CLOUD_LOCATION_SERVER_ADDR", '"' + cloudLocationServerAddr + '"')
        buildConfigField("String", "CLOUD_SERVER_ADDR", '"' + cloudServerAddr + '"')
        buildConfigField("boolean", "CLOUD_SERVER_PROTOCOL_HTTPS", cloudServerProtoHttps)


        def archiveVersionName = ((System.getenv("ARCHIVE_NAME") as String ?: "HJT_SDKDEMO-") +
                                 (System.getenv("BUILD_VERSION") as String ?: "0.0"))

        project.ext.set("archivesBaseName", archiveVersionName)
        ndk {
            moduleName "jniLibs"
            abiFilters "armeabi", "armeabi-v7a"
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    signingConfigs {
        releaseConfig {
            // put something like code sign cert settings here
        }
    }

    useLibrary 'org.apache.http.legacy'
    buildTypes {
        release {
            signingConfig signingConfigs.releaseConfig
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INFTICE.txt'
        exclude 'META-INFTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

repositories {
    maven {
        // url 'https://download.01.org/crosswalk/releases/crosswalk/android/maven2'
        url 'http://maven.aliyun.com/nexus/content/repositories/releases/'
    }
    flatDir {
        dirs 'libs'   // aar目录
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation fileTree(include: ['*.aar'], dir: 'libs')
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:gridlayout-v7:28.0.0'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.andreabaccega:android-form-edittext:1.3.+'
    implementation 'com.lsjwzh:materialloadingprogressbar:0.5.8-RELEASE'
    implementation 'com.aliyun.ams:alicloud-android-man:1.2.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.15'
 //  implementation(name:'evsdk', ext:'aar')
    implementation 'cn.jiguang.sdk:jpush:3.3.4'
    implementation 'cn.jiguang.sdk:jcore:2.1.2'
    //华为
    implementation 'cn.jiguang.sdk.plugin:huawei:3.3.4'
    //小米
    implementation 'cn.jiguang.sdk.plugin:xiaomi:3.3.4'
    // oppo
    implementation 'cn.jiguang.sdk.plugin:oppo:3.3.4'
    //魅族
    implementation'cn.jiguang.sdk.plugin:meizu:3.3.4'
    //vivo
    implementation 'cn.jiguang.sdk.plugin:vivo:3.3.4'
}
